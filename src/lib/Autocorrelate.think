###
 # Autocorrelation
 # Do returns tend to persist in the same direction, reverse, or behave independently?
 #
 # Inputs:
 #   - data   = use log returns
 #   - offset = lag, usually 1
 #   - window
 #     - 20–50 bars  = short-term regime
 #     - 60–120 bars = swing/intermediate
 #     - 252 bars    = structural bias (rarely useful for trading)
 #
 # Interpretation:
 #   - positive  = persistence, trend-friendly
 #   - negative  = reversal, mean-reversion-friendly
 #   - near zero = randomness, noise, low edge
###

script Autocorrelate {
    input data   = 0;
    input offset = 5;
    input window = 20;

    def meanOne = fold indexOne = 0
        to   window
        with sumOne
        do   sumOne + GetValue(data, indexOne) / window;

    def meanTwo = GetValue(meanOne, offset);

    def meanAll = fold indexAll = 0
        to   window
        with sumAll
        do   sumAll + GetValue(data, indexAll) * GetValue(data, indexAll + offset) / window;

    def meanSqr = fold indexSqr = 0
        to   window
        with sumSqr
        do   sumSqr + Sqr(GetValue(data, indexSqr)) / window;

    def stdOne = Sqrt(Max(0, meanSqr - Sqr(meanOne)));
    def stdTwo = GetValue(stdOne, offset);

    plot autocorrelate;
    if !IsNaN(stdOne) and !IsNaN(stdTwo) and stdOne != 0 and stdTwo != 0 {
        autocorrelate = (meanAll - meanOne * meanTwo) / (stdOne * stdTwo);
    }
    else {
        autocorrelate = Double.NaN;
    }
}
