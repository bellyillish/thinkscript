#include_once "../lib/Date.think"
#include_once "../lib/Chart.think"

script Periods {
    input Period  = "Chart";
    input Since   = 20250407;
    input Candles = 100;

    def lastEarnings = Date().lastEarnings;
    def yyyy         = Date().yyyy;
    def yyyymm       = Date().yyyymm;
    def yyyymmOpex   = Date().yyyymmOpex;
    def yyyyq        = Date().yyyyq;
    def yyyyqOpex    = Date().yyyyqOpex;
    def yyyyww       = Date().yyyyww;
    def endBar       = Chart().endBar;
    def endOffset    = Chart().endOffset;
    def isStart      = Chart().isStart;

    def isNew;
    def isValid;
    def isLatest;
    if Period == "Daily" {
        isNew    = isStart or GetYYYYMMDD() != GetValue(GetYYYYMMDD(), 1);
        isLatest = GetYYYYMMDD() == GetValue(GetYYYYMMDD(), endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.TWO_HOURS;
    }
    else if Period == "Weekly" {
        isNew    = isStart or yyyyww != GetValue(yyyyww, 1);
        isLatest = yyyyww == GetValue(yyyyww, endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.DAY;
    }
    else if Period == "Monthly" {
        isNew    = isStart or yyyymm != GetValue(yyyymm, 1);
        isLatest = yyyymm == GetValue(yyyymm, endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.WEEK;
    }
    else if Period == "MonthlyOPEX" {
        isNew    = isStart or yyyymmOpex != GetValue(yyyymmOpex, 1);
        isLatest = yyyymmOpex == GetValue(yyyymmOpex, endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.WEEK;
    }
    else if Period == "Quarterly" {
        isNew    = isStart or yyyyq != GetValue(yyyyq, 1);
        isLatest = yyyyq == GetValue(yyyyq, endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.MONTH;
    }
    else if Period == "QuarterlyOPEX" {
        isNew    = isStart or yyyyqOpex != GetValue(yyyyqOpex, 1);
        isLatest = yyyyqOpex == GetValue(yyyyqOpex, endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.MONTH;
    }
    else if Period == "Earnings" {
        isNew    = isStart or lastEarnings != GetValue(lastEarnings, 1);
        isLatest = lastEarnings == GetValue(lastEarnings, endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.MONTH;
    }
    else if Period == "Yearly" {
        isNew    = isStart or yyyy != GetValue(yyyy, 1);
        isLatest = yyyy == GetValue(yyyy, endOffset);
        isValid  = GetAggregationPeriod() <= AggregationPeriod.QUARTER;
    }
    else if Period == "SinceDate" {
        isNew    = GetYYYYMMDD() >= Since and (isStart or GetValue(GetYYYYMMDD(), 1) < Since);
        isLatest = GetYYYYMMDD() >= Since;
        isValid  = yes;
    }
    else if Period == "CandlesAgo" {
        isNew    = isStart and Candles > endBar or BarNumber() + Candles == endBar;
        isLatest = BarNumber() >= endBar - Candles;
        isValid  = yes;
    }
    else {
        isNew    = isStart;
        isValid  = yes;
        isLatest = yes;
    }

    def isActive      = isNew or GetValue(isActive, 1);
    def isNewAndValid = isNew and isValid;

    plot P = isNew;
    P.Hide();
}
