declare upper;

#include_once "../lib/Date.think"
#include_once "../lib/Chart.think"


### SCRIPTS ###
    script ExpectedMove {
        input Price   = 0;
        input Expires = 0;
        input Vol     = 0;
        input Days    = 365;

        def inputPrice   = Price;
        def inputExpires = Expires;
        def inputVol     = Vol;
        def inputDays    = Days;

        def lastPrice   = GetValue(inputPrice,   Chart().endOffset);
        def lastExpires = GetValue(inputExpires, Chart().endOffset);
        def lastVol     = GetValue(inputVol,     Chart().endOffset);
        def lastDays    = GetValue(inputDays,    Chart().endOffset);

        plot Move = lastPrice * lastVol * Sqrt(lastExpires / lastDays);
        plot H    = lastPrice + Move;
        plot L    = lastPrice - Move;
    }
###


### INPUTS ###
    input Period = {
        "Intraday",
        "Daily",
        "Weekly",
        default "Monthly",
        "Quarterly"
    };

    input ShowOptions      = yes;
    input ShowImplied      = no;
    input ShowHistorical   = no;
    input HistoricalPeriod = 20;
    input Labels           = yes;
    input LabelSize        = FontSize.SMALL;
    input Extend           = no;
    input Separators       = no;
###


### CALCULATIONS ####
    def hasOptions  = !IsNaN(SeriesVolatility(series = 1, expirationType = ExpirationType.REGULAR));
    def hasWeeklies = !IsNaN(SeriesVolatility(series = 6, expirationType = ExpirationType.WEEKLYS));
    def endOffset   = Chart().endOffset;

    def isLastPeriod;
    switch (Period) {
        case "Quarterly":
            isLastPeriod = Date().yyyyqOpex == GetValue(Date().yyyyqOpex, endOffset);
        case "Monthly":
            isLastPeriod = Date().yyyymmOpex == GetValue(Date().yyyymmOpex, endOffset);
        case "Weekly":
            isLastPeriod = Date().yyyyww == GetValue(Date().yyyyww, endOffset);
        default:
            isLastPeriod = Date() == GetValue(Date(), endOffset);
    }

    def series;
    switch (Period) {
        case "Quarterly":
            series = (1 - Date().mmOpex) % 3 + 6;
        case "Monthly":
            series = 2;
        case "Weekly":
            series = 7 - Date().dow - Date().isOpexWeek;
        case "Daily":
            series = 2;
        case "Intraday":
            series = 1;
    }

    def calDays;
    def tradeDays;
    switch (Period) {
        case "Quarterly":
            calDays   = Next3rdFriday(series - Date().isOpexDay);
            tradeDays = Ceil(calDays * 253 / 365);
        case "Monthly":
            calDays   = Next3rdFriday(series - Date().isOpexDay);
            tradeDays = Ceil(calDays * 253 / 365);
        case "Weekly":
            calDays   = 8 - Date().dow;
            tradeDays = calDays - 2;
        case "Daily":
            calDays   = 1;
            tradeDays = calDays;
        case "Intraday":
            calDays   = (RegularTradingEnd(GetYYYYMMDD()) - GetTime()) / (24 * 60 * 60000);
            tradeDays = (RegularTradingEnd(GetYYYYMMDD()) - GetTime()) / (RegularTradingEnd(GetYYYYMMDD()) - RegularTradingStart(GetYYYYMMDD()));
    }

    def ov;
    switch (Period) {
        case "Quarterly":
            if series == 6 {
                ov = SeriesVolatility(series = 6, expirationType = ExpirationType.REGULAR);
            }
            else if series == 5 {
                ov = SeriesVolatility(series = 5, expirationType = ExpirationType.REGULAR);
            }
            else if series == 4 {
                ov = SeriesVolatility(series = 4, expirationType = ExpirationType.REGULAR);
            }
            else {
                ov = Double.NaN;
            }
        case "Monthly":
            ov = SeriesVolatility(series = 2, expirationType = ExpirationType.REGULAR);
        case "Weekly":
            if series == 6 {
                ov = SeriesVolatility(series = 6, expirationType = ExpirationType.WEEKLYS);
            }
            else if series == 5 {
                ov = SeriesVolatility(series = 5, expirationType = ExpirationType.WEEKLYS);
            }
            else if series == 4 {
                ov = SeriesVolatility(series = 4, expirationType = ExpirationType.WEEKLYS);
            }
            else if series == 3 {
                ov = SeriesVolatility(series = 3, expirationType = ExpirationType.WEEKLYS);
            }
            else if series == 2 {
                ov = SeriesVolatility(series = 2, expirationType = ExpirationType.WEEKLYS);
            }
            else if series == 1 {
                ov = SeriesVolatility(series = 1, expirationType = ExpirationType.WEEKLYS);
            }
            else {
                ov = Double.NaN;
            }
        case "Daily":
            ov = SeriesVolatility(series = 2, expirationType = ExpirationType.WEEKLYS);
        case "Intraday":
            ov = SeriesVolatility(series = 1, expirationType = ExpirationType.WEEKLYS);
    }

    def iv = imp_volatility;
    def hv = HistoricalVolatility(HistoricalPeriod);

    def hasMove = (isLastPeriod or Extend)
        and (Period != Period.Intraday or GetAggregationPeriod() < AggregationPeriod.DAY)
        and (hasWeeklies or Period == Period.Monthly or Period == Period.Quarterly);

    def ol;
    def oh;
    def il;
    def ih;
    def hl;
    def hh;
    if hasMove {
        ol = ExpectedMove(close, calDays,   ov, 365).L;
        oh = ExpectedMove(close, calDays,   ov, 365).H;
        il = ExpectedMove(close, calDays,   iv, 365).L;
        ih = ExpectedMove(close, calDays,   iv, 365).H;
        hl = ExpectedMove(close, tradeDays, hv, 253).L;
        hh = ExpectedMove(close, tradeDays, hv, 253).H;
    }
    else {
        ol = Double.NaN;
        oh = Double.NaN;
        il = Double.NaN;
        ih = Double.NaN;
        hl = Double.NaN;
        hh = Double.NaN;
    }
###


### PLOTS ###
    DefineGlobalColor("Line",      CreateColor(48, 48, 48));
    DefineGlobalColor("Separators", CreateColor(32, 32, 32));

    plot OptL = ol;
    OptL.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    OptL.DefineColor("Bar", CreateColor(77, 166, 255));
    OptL.SetDefaultColor(OptL.Color("Bar"));
    OptL.SetHiding(!ShowOptions);

    plot OptH = oh;
    OptH.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    OptH.SetDefaultColor(OptL.Color("Bar"));
    OptH.SetHiding(!ShowOptions);

    plot ImpL = il;
    ImpL.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    ImpL.DefineColor("Bar", CreateColor(255, 159, 67));
    ImpL.SetDefaultColor(ImpL.Color("Bar"));
    ImpL.SetHiding(!ShowImplied);

    plot ImpH = ih;
    ImpH.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    ImpH.SetDefaultColor(ImpL.Color("Bar"));
    ImpH.SetHiding(!ShowImplied);

    plot HistL = hl;
    HistL.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    HistL.DefineColor("Bar", CreateColor(180, 180, 180));
    HistL.SetDefaultColor(HistL.Color("Bar"));
    HistL.SetHiding(!ShowHistorical);

    plot HistH = hh;
    HistH.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    HistH.SetDefaultColor(HistL.Color("Bar"));
    HistH.SetHiding(!ShowHistorical);

    # Lines
    plot Mid = if isLastPeriod or Extend then GetValue(close, endOffset) else Double.NaN;
    Mid.SetPaintingStrategy(PaintingStrategy.DASHES);
    Mid.SetDefaultColor(GlobalColor("Line"));
    Mid.HideBubble();
    Mid.HideTitle();

    # Dividers
    AddVerticalLine(
        visible = Separators and isLastPeriod != isLastPeriod[1],
        color   = GlobalColor("Separators"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );

    # Labels
    AddLabel(
        visible  = Labels and ShowHistorical,
        size     = LabelSize,
        color    = HistL.Color("Bar"),
        text     = " HV: " + AsPercent(hv) + " ",
        location = Location.TOP_RIGHT
    );
    AddLabel(
        visible  = Labels and ShowImplied,
        size     = LabelSize,
        color    = ImpL.Color("Bar"),
        text     = " IV: " + AsPercent(iv) + " ",
        location = Location.TOP_RIGHT
    );
    AddLabel(
        visible  = Labels and ShowOptions,
        size     = LabelSize,
        color    = OptL.Color("Bar"),
        text     = " OV: " + AsPercent(ov) + " ",
        location = Location.TOP_RIGHT
    );
###
