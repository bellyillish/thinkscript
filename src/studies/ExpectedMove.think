###
 # Expected Move
 # Quarterly:
 #   - P = current spot price
 #   - V = IV for the option series that expires on 3/20/26 (back-month quarterly option series)
 #   - E = 95 (calendar days until the 3/20/26 option series expires)
 # Monthly:
 #   - P = current spot price
 #   - V = IV for the option series that expires on 1/16/26 (back-month monthly option series)
 #   - E = 32 (calendar days until the 1/16/26 option series expires)
 # Weekly:
 #   - P = current spot price
 #   - V = IV for the option series that expires on 12/22/25 (weekly option series for the following Monday)
 #   - E = 7 (calendar days until the 12/22/25 option series expires)
 # Daily:
 #   - P = current spot price
 #   - V = IV for the option series that expires on 12/16/25 (weekly 1-day option for the next day)
 #   - E = 1 (calendar days until the 12/16/25 option series expires)
 # Intraday:
 #   - P = open price from current morning, 12/15/25
 #   - V = IV for the option series that expires on 12/15/25 (weekly 0-day option series for today)
 #   - E = 0.21 (5hrs left in day / 24hrs hours in day)
 # Historical volatility:
 #   - Use trading days (252) and trading hours (6.5)
###

declare upper;

#include_once "../lib/Date.think"
#include_once "../lib/Chart.think"

### SCRIPTS ###
    script ExpectedMove {
        input Price   = 0;
        input Expires = 0;
        input Vol     = 0;
        input Days    = 365;

        plot Move = Price * Vol * Sqrt(Expires / Days);
        plot H    = Price + Move;
        plot L    = Price - Move;
    }
###

### INPUTS ###
    input Period = {
        "Intraday",
        "Daily",
        "Weekly",
        default "Monthly",
        "Quarterly"
    };

    input Type = {
        default "Spot",
        "Anchored",
        "Price Anchored"
    };

    input Anchor = {
        default "Previous Close",
        "Current Open"
    };

    input Volatility = {
        default "Series",
        "Implied",
        "Historical"
    };

    input HistoricalPeriod = 20;
    input Labels           = yes;
    input LabelSize        = FontSize.SMALL;
    input Extend           = no;
    input Separators       = no;
###

### CALCULATIONS ####
    def hasOptions  = !IsNaN(SeriesVolatility(series = 1, expirationType = ExpirationType.REGULAR));
    def hasWeeklies = !IsNaN(SeriesVolatility(series = 6, expirationType = ExpirationType.WEEKLYS));
    def endOffset   = Chart().endOffset;

    def isLastPeriod;
    switch (Period) {
        case "Quarterly":
            isLastPeriod = Date().yyyyqOpex == GetValue(Date().yyyyqOpex, endOffset);
        case "Monthly":
            isLastPeriod = Date().yyyymmOpex == GetValue(Date().yyyymmOpex, endOffset);
        case "Weekly":
            isLastPeriod = Date().yyyyww == GetValue(Date().yyyyww, endOffset);
        default:
            isLastPeriod = Date() == GetValue(Date(), endOffset);
    }

    def fixOffset = if Anchor == Anchor."Current Open"
        then BarNumber() - HighestAll(BarNumber() * (isLastPeriod  and !GetValue(isLastPeriod, 1)))
        else BarNumber() - HighestAll(BarNumber() * (!isLastPeriod and isLastPeriod[-1]));

    # Option Series
    def series;
    switch (Period) {
        case "Quarterly":
            series = (1 - Date().mmOpex) % 3 + 6;
        case "Monthly":
            series = 2;
        case "Weekly":
            series = 7 - Date().dow - Date().isOpexWeek;
        case "Daily":
            series = 2;
        case "Intraday":
            series = 1;
    }

    def fixSeries = GetValue(series, endOffset);

    # Expiration Days
    def calDays;
    def tradeDays;
    switch (Period) {
        case "Quarterly":
            calDays   = Next3rdFriday(series - Date().isOpexDay);
            tradeDays = Ceil(calDays * 252 / 365);
        case "Monthly":
            calDays   = Next3rdFriday(series - Date().isOpexDay);
            tradeDays = Ceil(calDays * 252 / 365);
        case "Weekly":
            calDays   = 8 - Date().dow;
            tradeDays = calDays - 2;
        case "Daily":
            calDays   = 1;
            tradeDays = calDays;
        case "Intraday":
            calDays   = (RegularTradingEnd(GetYYYYMMDD()) - GetTime()) / (24 * 60 * 60000);
            tradeDays = (RegularTradingEnd(GetYYYYMMDD()) - GetTime()) / (RegularTradingEnd(GetYYYYMMDD()) - RegularTradingStart(GetYYYYMMDD()));
    }

    def expDays = if Volatility == Volatility.Historical
        then tradeDays
        else calDays;

    def totalDays = if Volatility == Volatility.Historical
        then 365
        else 252;

    def fixDays = if Type == Type.Anchored
        then GetValue(expDays, fixOffset)
        else GetValue(expDays, endOffset);

    # Implied Volatility
    def iv;
    switch (Volatility) {
        case "Series":
            switch (Period) {
                case "Quarterly":
                    if fixSeries == 6 {
                        iv = SeriesVolatility(series = 6, expirationType = ExpirationType.REGULAR);
                    }
                    else if fixSeries == 5 {
                        iv = SeriesVolatility(series = 5, expirationType = ExpirationType.REGULAR);
                    }
                    else if fixSeries == 4 {
                        iv = SeriesVolatility(series = 4, expirationType = ExpirationType.REGULAR);
                    }
                    else {
                        iv = Double.NaN;
                    }
                case "Monthly":
                    iv = SeriesVolatility(series = 2, expirationType = ExpirationType.REGULAR);
                case "Weekly":
                    if fixSeries == 6 {
                        iv = SeriesVolatility(series = 6, expirationType = ExpirationType.WEEKLYS);
                    }
                    else if fixSeries == 5 {
                        iv = SeriesVolatility(series = 5, expirationType = ExpirationType.WEEKLYS);
                    }
                    else if fixSeries == 4 {
                        iv = SeriesVolatility(series = 4, expirationType = ExpirationType.WEEKLYS);
                    }
                    else if fixSeries == 3 {
                        iv = SeriesVolatility(series = 3, expirationType = ExpirationType.WEEKLYS);
                    }
                    else if fixSeries == 2 {
                        iv = SeriesVolatility(series = 2, expirationType = ExpirationType.WEEKLYS);
                    }
                    else if fixSeries == 1 {
                        iv = SeriesVolatility(series = 1, expirationType = ExpirationType.WEEKLYS);
                    }
                    else {
                        iv = Double.NaN;
                    }
                case "Daily":
                    iv = SeriesVolatility(series = 2, expirationType = ExpirationType.WEEKLYS);
                case "Intraday":
                    iv = SeriesVolatility(series = 1, expirationType = ExpirationType.WEEKLYS);
            }
        case "Implied":
            iv = imp_volatility;
        case "Historical":
            iv = HistoricalVolatility(HistoricalPeriod);
    }

    def fixIV = if Type == Type.Anchored
        then GetValue(iv, fixOffset)
        else GetValue(iv, endOffset);

    # Price
    def price = if Anchor == Anchor."Current Open"
        then open
        else close;

    def fixPrice = if Type == Type.Spot
        then GetValue(price, endOffset)
        else GetValue(price, fixOffset);

    # Expected Move
    def move;
    def l;
    def h;
    if !isLastPeriod and !Extend {
        move = Double.NaN;
        l    = Double.NaN;
        h    = Double.NaN;
    }
    else if Period == Period.Intraday and GetAggregationPeriod() >= AggregationPeriod.DAY {
        move = Double.NaN;
        l    = Double.NaN;
        h    = Double.NaN;
    }
    else if Volatility == Volatility.Series and !hasOptions {
        move = Double.NaN;
        l    = Double.NaN;
        h    = Double.NaN;
    }
    else if Volatility == Volatility.Series and !hasWeeklies and Period != Period.Monthly and Period != Period.Quarterly {
        move = Double.NaN;
        l    = Double.NaN;
        h    = Double.NaN;
    }
    else {
        move = ExpectedMove(fixPrice, fixDays, fixIV, totalDays);
        l    = fixPrice - move;
        h    = fixPrice + move;
    }
###

### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separators", CreateColor(32, 32, 32));

    plot Lo = l;
    Lo.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    Lo.DefineColor("Bar", CreateColor(180, 180, 180));
    Lo.SetDefaultColor(Lo.Color("Bar"));

    plot Hi = h;
    Hi.SetPaintingStrategy(PaintingStrategy.HORIZONTAL);
    Hi.SetDefaultColor(Lo.Color("Bar"));

    # Lines
    plot Mid = if isLastPeriod or Extend then fixPrice else Double.NaN;
    Mid.SetPaintingStrategy(PaintingStrategy.DASHES);
    Mid.SetDefaultColor(GlobalColor("Line"));
    Mid.HideBubble();
    Mid.HideTitle();

    # Dividers
    AddVerticalLine(
        visible = Separators and isLastPeriod != GetValue(isLastPeriod, 1),
        color   = GlobalColor("Separators"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );

    # Labels
    AddLabel(
        visible  = Labels,
        size     = LabelSize,
        color    = Lo.Color("Bar"),
        text     = " IV: " + AsPercent(fixIV) + " (" + AsDollars(move) + ") ",
        location = Location.TOP_RIGHT
    );
###
