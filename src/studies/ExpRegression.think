declare upper;

#include_once "../lib/Value.think"

### INPUTS ###
    input Quote = {
        default "Close",
        "Open",
        "High",
        "Low",
        "HL2",
        "HLC3",
        "OHLC4",
        "VWAP"
    };

    @Date input SinceDate = 19020101;
    input Offset          = 0.0;
    input Deviations      = 0.0;
    input Sanitize        = no;
    input ExtendLeft      = no;
    input ExtendRight     = no;
###

### LOOKUPS ###
    def type;
    switch (Quote) {
        case "Close" : type = FundamentalType.CLOSE;
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
        case "VWAP"  : type = FundamentalType.VWAP;
    }

    def data;
    if Sanitize {
        data = Value(Fundamental(type));
    }
    else {
        data = Fundamental(type);
    }
###

### CALCULATIONS ###
    def linReg = InertiaAll(
        data          = Log(data * (1 + Offset / 100)),
        startDate     = SinceDate,
        extendToLeft  = ExtendLeft,
        extendToRight = ExtendRight
    );

    def expReg = Exp(linReg);

    def dev = StDevAll(
        data          = Log(data) - linReg,
        StartDate     = SinceDate,
        extendToLeft  = ExtendLeft,
        extendToRight = ExtendRight
    );
###

### PLOTS ###
  plot Regression = expReg;
  Regression.DefineColor("Value", CreateColor(160, 160, 160));
  Regression.SetDefaultColor(Regression.Color("Value"));

  ### Deviations
  plot Upper = expReg * Exp(Deviations * dev);
  Upper.SetHiding(Deviations < 1);
  Upper.DefineColor("Value", CreateColor(48, 48, 48));
  Upper.SetDefaultColor(Upper.Color("Value"));

  plot Lower = expReg / Exp(Deviations * dev);
  Lower.SetHiding(Deviations < 1);
  Lower.DefineColor("Value", CreateColor(48, 48, 48));
  Lower.SetDefaultColor(Lower.Color("Value"));
###
