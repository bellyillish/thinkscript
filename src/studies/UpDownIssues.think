declare lower;

#include_once "../lib/Chart.think"
#include_once "../lib/Value.think"
#include_once "../lib/Oscillate.think"
#include_once "../lib/Smooth.think"
#include_once "../lib/Periods.think"
#include_once "../lib/ZScore.think"

### INPUTS ###
    input Market = {
        default "ALLUSA",
        "NYSE",
        "NASDAQ",
        "S&P 500",
        "NASDAQ 100",
        "RUSSELL 2000",
        "DJIA"
    };

    input Quote = {
        default "Close",
        "Open",
        "High",
        "Low",
        "HL2",
        "HLC3",
        "OHLC4"
    };

    input Display = {
        default "Difference",
        "Log Ratio",
        "Percentage",
        "Cumulative",
        "Tick",
        "Z-Score",
        "Z-Sum"
    };

    input Period = {
        default "Chart",
        "Daily",
        "Weekly",
        "Monthly",
        "MonthlyOPEX",
        "Quarterly",
        "QuarterlyOPEX",
        "Earnings",
        "Yearly",
        "CandlesAgo",
        "SinceDate"
    };

    @Date input SinceDate = 20250407;
    input CandlesAgo      = 100;
    input Separators      = yes;
    input Sanitize        = no;
    input Oscillation     = 0;
    input Smoothing       = 0;
###

### LOOKUPS ###
    def type;
    switch (Quote) {
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
        default      : type = FundamentalType.CLOSE;
    }

    # Lookup
    def rawUp;
    def rawDn;
    def rawAll;
    switch (Market) {
        case "ALLUSA":
            rawUp  = Fundamental(type, "$ADVUS");
            rawDn  = Fundamental(type, "$DECLUS");
            rawAll = close("$UNCHUS") + close("$ADVUS") + close("$DECLUS");
        case "NYSE":
            rawUp  = Fundamental(type, "$ADVN");
            rawDn  = Fundamental(type, "$DECN");
            rawAll = close("$UNCN") + close("$ADVN") + close("$DECN");
        case "NASDAQ":
            rawUp  = Fundamental(type, "$ADVN/Q");
            rawDn  = Fundamental(type, "$DECN/Q");
            rawAll = close("$UNCN/Q") + close("$ADVN/Q") + close("$DECN/Q");
        case "S&P 500":
            rawUp  = Fundamental(type, "$ADVSP");
            rawDn  = Fundamental(type, "$DECLSP");
            rawAll = close("$UNCHSP") + close("$ADVSP") + close("$DECLSP");
        case "NASDAQ 100":
            rawUp  = Fundamental(type, "$ADVND");
            rawDn  = Fundamental(type, "$DECLND");
            rawAll = close("$UNCHND") + close("$ADVND") + close("$DECLND");
        case "RUSSELL 2000":
            rawUp  = Fundamental(type, "$ADVRL");
            rawDn  = Fundamental(type, "$DECLRL");
            rawAll =  close("$UNCHRL") + close("$ADVRL") + close("$DECLRL");
        case "DJIA":
            rawUp  = Fundamental(type, "$ADVI");
            rawDn  = Fundamental(type, "$DECLI");
            rawAll = close("$UNCHI") + close("$ADVI") + close("$DECLI");
    }

    # Sanitize
    def up;
    def dn;
    def all;
    if Sanitize {
        up  = Value(rawUp);
        dn  = Value(rawDn);
        all = Value(rawAll);
    }
    else {
        up  = rawUp;
        dn  = rawDn;
        all = rawAll;
    }
###

### CALCULATIONS ###
    def isActivePeriod = Periods(Period, SinceDate, CandlesAgo).isActive;
    def isNewPeriod    = Periods(Period, SinceDate, CandlesAgo).isNewAndValid;

    # Tick
    def tickUp;
    def tickDn;
    if Chart().isIntrabar {
        tickUp  = up - GetValue(up, 1);
        tickDn  = dn - GetValue(dn, 1);
    }
    else {
        tickUp  = up;
        tickDn  = dn;
    }

    # Value
    def rawTick;
    switch (Display) {
        case "Difference"  : rawTick = up - dn;
        case "Percentage"  : rawTick = If(all != 0, up / all, 0);
        case "Log Ratio"   : rawTick = If(up > 0 and dn > 0, Log(up / dn), 0);
        case "Z-Score"     : rawTick = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        case "Z-Sum"       : rawTick = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        default            : rawTick = tickUp - tickDn;
    }

    # Cumulative
    def rawSum;
    if IsNaN(rawTick) {
        rawSum = GetValue(rawSum, 1);
    }
    else if isNewPeriod {
        rawSum = rawTick;
    }
    else {
        rawSum = GetValue(rawSum, 1) + rawTick;
    }

    # Filters
    def sum;
    def tick;
    if !Chart().isValidBar or !isActivePeriod or IsNaN(rawTick) {
        tick = Double.NaN;
        sum  = Double.NaN;
    }
    else {
        tick = Smooth(Oscillate(rawTick, Oscillation), Smoothing);
        sum  = Smooth(Oscillate(rawSum,  Oscillation), Smoothing);
    }

    # Neutral
    def neutral;
    switch (Display) {
        case "Percentage" : neutral = 0.5;
        default           : neutral = 0;
    }
###

### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # Value
    plot UD = if Display == Display.Cumulative or Display == Display."Z-Sum" then sum else tick;
    UD.DefineColor("Up", Color.UPTICK);
    UD.DefineColor("Down", Color.DOWNTICK);
    UD.DefineColor("Neutral", Color.DARK_GRAY);
    UD.SetDefaultColor(UD.Color("Neutral"));
    UD.AssignValueColor(
        if Separators and isNewPeriod and GetValue(isActivePeriod, 1)
            then GlobalColor("Background")
        else if UD > neutral
            then UD.Color("Up")
        else if UD < neutral
            then UD.Color("Down")
            else UD.Color("Neutral")
    );

    # Line
    plot N = neutral;
    N.SetDefaultColor(GlobalColor("Line"));
    N.HideBubble();
    N.HideTitle();

    # Separators
    AddVerticalLine(
        visible = Separators and isNewPeriod and GetValue(isActivePeriod, 1),
        color   = GlobalColor("Separator"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );
###
