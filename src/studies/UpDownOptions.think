declare lower;

#include_once "../lib/Chart.think"
#include_once "../lib/Value.think"
#include_once "../lib/Filters.think"
#include_once "../lib/Periods.think"
#include_once "../lib/ZScore.think"

### INPUTS ###
    input Market = {
        default "ALLUSA",
        "CBOE Total",
        "CBOE Index",
        "CBOE Equities",
        "NYSE",
        "NASDAQ",
        "S&P 500",
        "NASDAQ 100",
        "RUSSELL 2000",
        "DJIA"
    };

    input Quote = {
        default "Close",
        "Open",
        "High",
        "Low",
        "HL2",
        "HLC3",
        "OHLC4",
        "VWAP"
    };

    input Display = {
        default "Difference",
        "Percentage",
        "Log Ratio",
        "Cumulative",
        "Stack",
        "Tick",
        "Z-Score",
        "Z-Sum"
    };

    input Period = {
        default "Chart",
        "Daily",
        "Weekly",
        "Monthly",
        "MonthlyOPEX",
        "Quarterly",
        "QuarterlyOPEX",
        "Earnings",
        "Yearly",
        "CandlesAgo",
        "SinceDate"
    };

    @Date input SinceDate = 20250407;
    input CandlesAgo      = 100;
    input Separators      = yes;
    input Sanitize        = no;
    input Invert          = no;
    input Oscillation     = 0;
    input Smoothing       = 0;
###

### LOOKUPS ###
    def type;
    switch (Quote) {
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
        case "VWAP"  : type = FundamentalType.VWAP;
        default      : type = FundamentalType.CLOSE;
    }

    def rawAll;
    def rawTicks;
    def rawRatio;
    def rawMean;
    switch (Market) {
        case "ALLUSA":
            rawAll   = volume("$PCALL");
            rawTicks = tick_count("$PCALL");
            rawRatio = Fundamental(type, "$PCALL");
            rawMean  = 0.86;
        case "CBOE Total":
            rawAll   = volume("$CPC");
            rawTicks = tick_count("$CPC");
            rawRatio = Fundamental(type, "$CPC");
            rawMean  = 0.94;
        case "CBOE Index":
            rawAll  = volume("$CPCI");
            rawTicks = tick_count("$CPCI");
            rawRatio = Fundamental(type, "$CPCI");
            rawMean  = 1.30;
        case "CBOE Equities":
            rawAll  = volume("$CPCE");
            rawTicks = tick_count("$CPCE");
            rawRatio = Fundamental(type, "$CPCE");
            rawMean  = 0.78;
        case "NYSE":
            rawAll  = volume("$PCN");
            rawTicks = tick_count("$PCN");
            rawRatio = Fundamental(type, "$PCN");
            rawMean  = 0.66;
        case "NASDAQ":
            rawAll  = volume("$PCN/Q");
            rawTicks = tick_count("$PCN/Q");
            rawRatio = Fundamental(type, "$PCN/Q");
            rawMean  = 0.73;
        case "S&P 500":
            rawAll  = volume("$PCSP");
            rawTicks = tick_count("$PCSP");
            rawRatio = Fundamental(type, "$PCSP");
            rawMean  = 0.78;
        case "NASDAQ 100":
            rawAll  = volume("$PCND");
            rawTicks = tick_count("$PCND");
            rawRatio = Fundamental(type, "$PCND");
            rawMean  = 0.79;
        case "RUSSELL 2000":
            rawAll  = volume("$PCRL");
            rawTicks = tick_count("$PCRL");
            rawRatio = Fundamental(type, "$PCRL");
            rawMean  = 0.62;
        case "DJIA":
            rawAll  = volume("$PCI");
            rawTicks = tick_count("$PCI");
            rawRatio = Fundamental(type, "$PCI");
            rawMean  = 0.64;
    };

    def dataAll;
    def dataTicks;
    def dataRatio;
    if Sanitize {
        dataAll   = Value(rawAll);
        dataTicks = Value(rawTicks);
        dataRatio = Value(rawRatio);
    }
    else {
        dataAll   = rawAll;
        dataTicks = rawTicks;
        dataRatio = rawRatio;
    }
###

### CALCULATIONS ###
    def isNewPeriod    = Periods(Period, SinceDate, CandlesAgo).isNewAndValid;
    def isActivePeriod = Periods(Period, SinceDate, CandlesAgo).isActive;

    # Data Fixes
    def all;
    if Chart().isIntraday {
        all = Round(dataAll * HighestAll(dataTicks) / dataTicks, 0);
    }
    else {
        all = dataAll;
    }

    # Puts/calls
    def calls = Round(all / (dataRatio + 1), 0);
    def puts  = all - calls;

    # Tick
    def tickCalls;
    def tickPuts;
    def tickAll;
    if Chart().isIntrabar {
        tickCalls = Max(0, calls - GetValue(calls, 1));
        tickPuts  = Max(0, puts  - GetValue(puts, 1));
        tickAll   = Max(0, all   - GetValue(all, 1));
    }
    else {
        tickCalls = calls;
        tickPuts  = puts;
        tickAll   = all;
    }

    # Invert
    def tickUp;
    def tickDn;
    def up;
    def dn;
    def mean;
    if Invert {
        tickUp = tickCalls;
        tickDn = tickPuts;
        up     = calls;
        dn     = puts;
        mean   = 1 / rawMean;
    }
    else {
        tickUp = tickPuts;
        tickDn = tickCalls;
        up     = puts;
        dn     = calls;
        mean   = rawMean;
    }

    # Value
    def rawTick;
    switch (Display) {
        case "Difference"  : rawTick = up - dn;
        case "Percentage"  : rawTick = If(all != 0, up / all, 0);
        case "Log Ratio"   : rawTick = If(up > 0 and dn > 0, Log(up / dn), 0);
        case "Z-Score"     : rawTick = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        case "Z-Sum"       : rawTick = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        default            : rawTick = tickUp - tickDn;
    }

    # Cumulative
    def rawSum;
    if IsNaN(rawTick) {
        rawSum = GetValue(rawSum, 1);
    }
    else if isNewPeriod {
        rawSum = rawTick;
    }
    else {
        rawSum = GetValue(rawSum, 1) + rawTick;
    }

    # Filters
    def sum;
    def tick;
    if !Chart().isValidBar or !isActivePeriod or IsNaN(rawTick) {
        tick = Double.NaN;
        sum  = Double.NaN;
    }
    else {
        tick = Filters(rawTick, Oscillation, Smoothing);
        sum  = Filters(rawSum,  Oscillation, Smoothing);
    }

    # Neutral
    def neutral;
    switch (Display) {
        case "Percentage" : neutral = mean / (1 + mean);
        case "Log Ratio"  : neutral = If(mean > 0, Log(mean), Double.NaN);
        default           : neutral = 0;
    }
###

### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # Value
    plot UD = if Display == Display.Cumulative or Display == Display."Z-Sum" then sum else tick;
    UD.SetHiding(Display == Display.Stack);
    UD.DefineColor("Up", Color.UPTICK);
    UD.DefineColor("Down", Color.DOWNTICK);
    UD.DefineColor("Neutral", Color.DARK_GRAY);
    UD.SetDefaultColor(UD.Color("Neutral"));
    UD.AssignValueColor(
        if Separators and isNewPeriod and GetValue(isActivePeriod, 1)
            then GlobalColor("Background")
        else if UD > neutral
            then UD.Color("Up")
        else if UD < neutral
            then UD.Color("Down")
            else UD.Color("Neutral")
    );

    # Stack
    plot GU = tickCalls;
    GU.SetHiding(Display != Display.Stack);
    GU.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    GU.DefineColor("Value", Color.UPTICK);
    GU.SetDefaultColor(GU.Color("Value"));

    plot GD = -tickPuts;
    GD.SetHiding(Display != Display.Stack);
    GD.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    GD.DefineColor("Value", Color.DOWNTICK);
    GD.SetDefaultColor(GD.Color("Value"));

    # Line
    plot N = neutral;
    N.SetDefaultColor(GlobalColor("Line"));
    N.HideBubble();
    N.HideTitle();

    # Separators
    AddVerticalLine(
        visible = Separators and isNewPeriod and GetValue(isActivePeriod, 1),
        color   = GlobalColor("Separator"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );
###
