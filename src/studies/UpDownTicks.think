declare lower;

#include_once "../lib/Chart.think"
#include_once "../lib/Sanitizer.think"
#include_once "../lib/Oscillate.think"
#include_once "../lib/Periods.think"
#include_once "../lib/ZScore.think"

### INPUTS ###
    input Market = {
        default "ALLUSA",
        "NYSE",
        "NASDAQ",
        "S&P 500",
        "NASDAQ 100",
        "RUSSELL 2000",
        "DJIA"
    };

    input Quote = {
        default "Close",
        "Open",
        "High",
        "Low",
        "HL2",
        "HLC3",
        "OHLC4"
    };

    input Display = {
        default "Difference",
        "Log Ratio",
        "Percentage",
        "Cumulative",
        "Z-Score",
        "Z-Sum"
    };

    input Period = {
        default "Chart",
        "Daily",
        "Weekly",
        "Monthly",
        "MonthlyOPEX",
        "Quarterly",
        "QuarterlyOPEX",
        "Earnings",
        "Yearly",
        "CandlesAgo",
        "SinceDate"
    };

    @Date input SinceDate = 20250407;
    input CandlesAgo      = 252;
    input Separators      = yes;
    input Sanitize        = no;
    input Oscillation     = 0;
###

### LOOKUPS ###
    def type;
    switch (Quote) {
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
        default      : type = FundamentalType.CLOSE;
    }

    # Lookup
    def rawTick;
    def rawAll;
    switch (Market) {
        case "ALLUSA":
            rawTick = Fundamental(type, "$TIKUS");
            rawAll  = close("$ADVUS") + close("$DECLUS") + close("$UNCHUS");
        case "NYSE":
            rawTick = Fundamental(type, "$TICK");
            rawAll  = close("$ADVN") + close("$DECN") + close("$UNCN");
        case "NASDAQ":
            rawTick = Fundamental(type, "$TICK/Q");
            rawAll  = close("$ADVN/Q") + close("$DECN/Q") + close("$UNCN/Q");
        case "S&P 500":
            rawTick = Fundamental(type, "$TIKSP");
            rawAll  = close("$ADVSP") + close("$DECLSP") + close("$UNCHSP");
        case "NASDAQ 100":
            rawTick = Fundamental(type, "$TIKND");
            rawAll  = close("$ADVND") + close("$DECLND") + close("$UNCHND");
        case "RUSSELL 2000":
            rawTick = Fundamental(type, "$TIKRL");
            rawAll  = close("$ADVRL") + close("$DECLRL") + close("$UNCHRL");
        case "DJIA":
            rawTick = Fundamental(type, "$TIKI");
            rawAll  = close("$ADVI") + close("$DECLI") + close("$UNCHI");
    }

    def tick;
    def all;
    if Sanitize {
        tick = Sanitizer(rawTick);
        all  = Sanitizer(rawAll);
    }
    else {
        tick = rawTick;
        all  = rawAll;
    }
###

### CALCULATIONS ###
    def isNewPeriod    = Periods(Period, SinceDate, CandlesAgo).isNewAndValid;
    def isActivePeriod = Periods(Period, SinceDate, CandlesAgo).isActive;

    # Up/Down tick
    def up = (all + tick) / 2;
    def dn = (all - tick) / 2;

    # Value
    def rawValue;
    switch (Display) {
        case "Percentage"  : rawValue = If(all != 0, up / all, 0);
        case "Log Ratio"   : rawValue = If(up > 0 and dn > 0, Log(up / dn), 0);
        case "Z-Score"     : rawValue = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        case "Z-Sum"       : rawValue = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        default            : rawValue = tick;
    }

    # Cumulative
    def rawSum;
    if IsNaN(rawValue) {
        rawSum = GetValue(rawSum, 1);
    }
    else if isNewPeriod {
        rawSum = rawValue;
    }
    else {
        rawSum = GetValue(rawSum, 1) + rawValue;
    }

    # Filters
    def sum;
    def value;
    if !Chart().isValidBar or !isActivePeriod or IsNaN(rawValue) {
        value = Double.NaN;
        sum   = Double.NaN;
    }
    else {
        value = Oscillate(rawValue, Oscillation);
        sum   = Oscillate(rawSum,   Oscillation);
    }

    # Neutral
    def neutral;
    switch (Display) {
        case "Percentage" : neutral = 0.5;
        default           : neutral = 0;
    }
###


### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # Value
    plot TK = if Display == Display.Cumulative or Display == Display."Z-Sum" then sum else value;
    TK.DefineColor("Up", Color.UPTICK);
    TK.DefineColor("Down", Color.DOWNTICK);
    TK.DefineColor("Neutral", Color.DARK_GRAY);
    TK.SetDefaultColor(TK.Color("Neutral"));
    TK.AssignValueColor(
        if Separators and isNewPeriod and GetValue(isActivePeriod, 1)
            then GlobalColor("Background")
        else if TK > neutral
            then TK.Color("Up")
        else if TK < neutral
            then TK.Color("Down")
            else TK.Color("Neutral")
    );

    # Line
    plot N = neutral;
    N.SetDefaultColor(GlobalColor("Line"));
    N.HideBubble();
    N.HideTitle();

    # Separators
    AddVerticalLine(
        visible = Separators and isNewPeriod and GetValue(isActivePeriod, 1),
        color   = GlobalColor("Separator"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );
###
