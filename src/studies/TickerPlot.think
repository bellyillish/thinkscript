declare lower;
declare real_size;

#include_once "../lib/Chart.think"
#include_once "../lib/Sanitizer.think"
#include_once "../lib/Oscillate.think"
#include_once "../lib/Period.think"
#include_once "../lib/ZScore.think"

### INPUTS ###
    input Ticker = "";

    input Quote = {
        default "Close",
        "Open",
        "High",
        "Low",
        "HL2",
        "HLC3",
        "OHLC4",
        "VWAP",
        "RTH",
        "ETH"
    };

    input Method = {
        default "Logarithmic",
        "ZScored",
        "Absolute"
    };

    input Display = {
        default "Sum",
        "Tick"
    };

    input Anchor = {
        default "Chart",
        "Daily",
        "Weekly",
        "Monthly",
        "MonthlyOPEX",
        "Quarterly",
        "QuarterlyOPEX",
        "Earnings",
        "Yearly",
        "CandlesAgo",
        "SinceDate"
    };

    @Date input SinceDate = 20250407;
    input CandlesAgo      = 252;
    input Separators      = yes;
    input Sanitize        = no;
    input Oscillation     = 0;
    input Length          = 60;
###

### LOOKUPS ###
    def type;
    switch (Quote) {
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
        case "VWAP"  : type = FundamentalType.VWAP;
        default      : type = FundamentalType.CLOSE;
    }

    def rawOpen;
    def rawClose;
    if Ticker != "" {
        rawOpen  = open(Ticker);
        rawClose = Fundamental(type, Ticker);
    }
    else {
        rawOpen = open;
        rawClose = Fundamental(type);
    }

    # Sanitize
    def dataOpen;
    def dataClose;
    if Sanitize {
        dataOpen  = Sanitizer(rawOpen);
        dataClose = Sanitizer(rawClose);
    }
    else {
        dataOpen  = rawOpen;
        dataClose = rawClose;
    }
###

### CALCULATIONS ###
    def isNewPeriod    = Period(Anchor, SinceDate, CandlesAgo).isNewAndValid;
    def isActivePeriod = Period(Anchor, SinceDate, CandlesAgo).isActive;

    # Logarithmic
    def logOpen;
    def logClose;
    if Method == Method.Absolute {
        logOpen  = dataOpen;
        logClose = dataClose;
    }
    else {
        logOpen  = If(dataOpen  > 0, Log(dataOpen),  0);
        logClose = If(dataClose > 0, Log(dataClose), 0);
    }

    def rawTick;
    if Quote == Quote.RTH and !Chart().isRTH {
        rawTick = 0;
    }
    else if Quote == Quote.RTH and !Chart().isIntrabar {
        rawTick = logClose - logOpen;
    }
    else if Quote == Quote.ETH and Chart().isRTH and !Chart().isIntrabar {
        rawTick = CompoundValue(1, logOpen - logClose[1], 0);
    }
    else if Quote == Quote.ETH and Chart().isRTH {
        rawTick = 0;
    }
    else {
        rawTick = CompoundValue(1, logClose - logClose[1], logClose - logOpen);
    }

    # Tick
    def zTick;
    if Method == Method.ZScored {
        zTick = ZScore(rawTick, Length);
    }
    else {
        zTick = rawTick;
    }

    # Cumulative
    def rawSum;
    if IsNaN(zTick) {
        rawSum = GetValue(rawSum, 1);
    }
    else if isNewPeriod {
        rawSum = zTick;
    }
    else {
        rawSum = GetValue(rawSum, 1) + zTick;
    }

    # Filters
    def sum;
    def tick;
    if !Chart().isValidBar or !isActivePeriod or IsNaN(zTick) {
        tick = Double.NaN;
        sum  = Double.NaN;
    }
    else {
        tick = Oscillate(zTick,  Oscillation);
        sum  = Oscillate(rawSum, Oscillation);
    }
###

### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # Tick
    plot T = tick;
    T.SetHiding(Display != Display.Tick);
    T.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    T.DefineColor("Up", Color.UPTICK);
    T.DefineColor("Down", Color.DOWNTICK);
    T.DefineColor("Neutral", Color.DARK_GRAY);
    T.SetDefaultColor(T.Color("Neutral"));
    T.AssignValueColor(
        if Separators and isNewPeriod and GetValue(isActivePeriod, 1)
            then GlobalColor("Background")
        else if tick > 0
            then T.Color("Up")
        else if tick < 0
            then T.Color("Down")
            else T.Color("Neutral")
    );

    # Cumulative
    plot S = sum;
    S.SetHiding(Display != Display.Sum);
    S.DefineColor("Up", Color.UPTICK);
    S.DefineColor("Down", Color.DOWNTICK);
    S.DefineColor("Neutral", Color.DARK_GRAY);
    S.SetDefaultColor(S.Color("Neutral"));
    S.AssignValueColor(
        if Separators and isNewPeriod and GetValue(isActivePeriod, 1)
            then GlobalColor("Background")
        else if tick > 0
            then S.Color("Up")
        else if tick < 0
            then S.Color("Down")
            else S.Color("Neutral")
    );

    # Line
    plot N = 0;
    N.SetDefaultColor(GlobalColor("Line"));
    N.HideBubble();
    N.HideTitle();
    N.Hide();

    # Separators
    AddVerticalLine(
        visible = Separators and isNewPeriod and GetValue(isActivePeriod, 1),
        color   = GlobalColor("Separator"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );
###
