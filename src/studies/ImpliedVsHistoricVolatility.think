declare lower;

#include_once "../lib/Oscillate.think"
#include_once "../lib/Smooth.think"
#include_once "../lib/ZScore.think"

### INPUTS ###
    input Display = {
        default "Ratio",
        "Overlay"
    };

    input ZScored     = no;
    input ZLength     = 90;
    input HLength     = 20;
    input Oscillation = 0;
    input Smoothing   = 0;
###

### LOOKUPS ###
    def rawVI = imp_volatility();
    def rawVH = HistoricalVolatility(HLength);
###

### CALCULATIONS ###
    def rawRatio;
    if rawVI > 0 and rawVH > 0 {
        rawRatio = Log(rawVI / rawVH);
    }
    else {
        rawRatio = Double.NaN;
    }

    def dataVI;
    def dataVH;
    def dataRatio;
    if ZScored {
        dataVI    = ZScore(rawVI,    ZLength);
        dataVH    = ZScore(rawVH,    ZLength);
        dataRatio = ZScore(rawRatio, ZLength);
    }
    else {
        dataVI    = rawVI;
        dataVH    = rawVH;
        dataRatio = rawRatio;
    }

    # Filters
    def vi    = Smooth(Oscillate(dataVI,    Oscillation), Smoothing);
    def vh    = Smooth(Oscillate(dataVH,    Oscillation), Smoothing);
    def ratio = Smooth(Oscillate(dataRatio, Oscillation), Smoothing);
###

### PLOTS ###
    DefineGlobalColor("Line", CreateColor(48, 48, 48));

    # Implied
    plot I = vi;
    I.SetHiding(Display != Display.Overlay);
    I.DefineColor("Value", Color.UPTICK);
    I.SetDefaultColor(I.Color("Value"));

    # Historical
    plot H = vh;
    H.SetHiding(Display != Display.Overlay);
    H.DefineColor("Value", Color.DOWNTICK);
    H.SetDefaultColor(H.Color("Value"));

    # Ratio
    plot R = ratio;
    R.SetHiding(Display != Display.Ratio);
    R.DefineColor("Neutral", CreateColor(96, 96, 96));
    R.SetDefaultColor(R.Color("Neutral"));
    R.AssignValueColor(
        if ratio > 0
            then I.Color("Value")
        else if ratio < 0
            then H.Color("Value")
            else R.Color("Neutral")
    );

    # Mid
    plot Mid = 0;
    Mid.SetDefaultColor(GlobalColor("Line"));
    Mid.HideBubble();
    Mid.HideTitle();
###
