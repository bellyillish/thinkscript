declare lower;
declare zerobase;

#include_once "../lib/Chart.think"
#include_once "../lib/Sanitizer.think"
#include_once "../lib/Oscillate.think"

### INPUTS ###
    input Source = {
        default "Ticker",
        "ALLUSA",
        "NYSE",
        "NASDAQ",
        "S&P 500",
        "NASDAQ 100",
        "RUSSELL 2000",
        "DJIA"
    };

    input Ticker = "";

    input Display = {
        default "Relative",
        "Total"
    };

    input Length     = 20;
    input Deviations = 2.0;
    input Negative   = yes;
    input Sanitize   = no;
###

### LOOKUPS ###
    # Lookup
    def rawVolume;
    def rawClose;
    switch (Source) {
        case "ALLUSA":
            rawVolume = close("$TVOLUS") * 1000;
            rawClose  = close("$DWCF");
        case "NYSE":
            rawVolume = close("$TVOL") * 1000;
            rawClose  = close("NYA");
        case "NASDAQ":
            rawVolume = close("$TVOL/Q") * 1000;
            rawClose  = close("$COMP");
        case "S&P 500":
            rawVolume = close("$TVOLSP") * 1000;
            rawClose  = close("SPX");
        case "NASDAQ 100":
            rawVolume = close("$TVOLND") * 1000;
            rawClose  = close("NDX");
        case "RUSSELL 2000":
            rawVolume = close("$TVOLRL") * 1000;
            rawClose  = close("RUT");
        case "DJIA":
            rawVolume = close("$TVOLI") * 1000;
            rawClose  = close("$DJI");
        default:
            if Ticker != "" {
                rawVolume = volume(Ticker);
                rawClose  = close(Ticker);
            }
            else {
                rawVolume = volume;
                rawClose  = close;
            }
    }

    # Sanitize
    def dataVolume;
    def dataClose;
    if Sanitize {
        dataVolume = Sanitizer(rawVolume);
        dataClose  = Sanitizer(rawClose);
    }
    else {
        dataVolume = rawVolume;
        dataClose  = rawClose;
    }
###

### CALCULATIONS ###
    # Internals
    def tickVolume;
    if Chart().isIntrabar and Source != Source.Ticker {
        tickVolume = CompoundValue(1, dataVolume - dataVolume[1], 0);
    }
    else {
        tickVolume = dataVolume;
    }

    # Relative
    def oscVolume;
    def volumeDevs;
    def relVolume;
    if Length > 1 {
        oscVolume  = Oscillate(tickVolume, Length);
        volumeDevs = StDev(tickVolume, Length);
        relVolume  =
            if volumeDevs == 0
                then 0
            else if !Negative
                then Max(0, oscVolume / volumeDevs)
                else oscVolume / volumeDevs;
    }
    else {
        oscVolume  = Double.NaN;
        volumeDevs = Double.NaN;
        relVolume  = Double.NaN;
    }
###

### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # Volume
    plot RV = if Display == Display.Relative then relVolume else tickVolume;
    RV.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    RV.DefineColor("AbnormalUp", Color.UPTICK);
    RV.DefineColor("AbnormalDn", Color.DOWNTICK);
    RV.DefineColor("Abnormal", CreateColor(160, 160, 160));
    RV.DefineColor("Positive", CreateColor(84, 84, 84));
    RV.DefineColor("Negative", CreateColor(48, 48, 48));
    RV.SetDefaultColor(RV.Color("Negative"));
    RV.AssignValueColor(
        if Deviations > 0 and relVolume >= Deviations and dataClose > GetValue(dataClose, 1)
            then RV.Color("AbnormalUp")
        else if Deviations > 0 and relVolume >= Deviations and dataClose < GetValue(dataClose, 1)
            then RV.Color("AbnormalDn")
        else if Deviations > 0 and relVolume >= Deviations
            then RV.Color("Abnormal")
        else if relVolume > 0
            then RV.Color("Positive")
            else RV.Color("Negative")
    );

    # Lines
    plot D = Deviations;
    D.SetHiding(Display != Display.Relative);
    D.SetDefaultColor(GlobalColor("Line"));
    D.HideBubble();
    D.HideTitle();

    plot N = 0;
    N.SetHiding(Display != Display.Relative or !Negative);
    N.SetDefaultColor(GlobalColor("Line"));
    N.HideBubble();
    N.HideTitle();
###
