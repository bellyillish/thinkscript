declare lower;

#include_once "../lib/Chart.think"
#include_once "../lib/Value.think"
#include_once "../lib/Oscillate.think"
#include_once "../lib/Smooth.think"
#include_once "../lib/Periods.think"
#include_once "../lib/ZScore.think"

### INPUTS ###
    input Market = {
        default "ALLUSA",
        "NYSE",
        "NASDAQ",
        "S&P 500",
        "NASDAQ 100",
        "RUSSELL 2000",
        "DJIA"
    };

    input Quote = {
        default "Close",
        "Open",
        "High",
        "Low",
        "HL2",
        "HLC3",
        "OHLC4"
    };

    input Display = {
        default "Difference",
        "Log Ratio",
        "Percentage",
        "Cumulative",
        "Stack",
        "Volume Graph",
        "Tick",
        "Z-Score",
        "Z-Sum"
    };

    input Period = {
        default "Chart",
        "Daily",
        "Weekly",
        "Monthly",
        "MonthlyOPEX",
        "Quarterly",
        "QuarterlyOPEX",
        "Earnings",
        "Yearly",
        "CandlesAgo",
        "SinceDate"
    };

    @Date input SinceDate = 20250407;
    input CandlesAgo      = 252;
    input Separators      = yes;
    input Sanitize        = no;
    input Oscillation     = 0;
    input Smoothing       = 0;
###

### LOOKUPS ###
    def type;
    switch (Quote) {
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
        default      : type = FundamentalType.CLOSE;
    }

    # Lookup
    def rawUp;
    def rawDn;
    def rawAll;
    switch (Market) {
        case "ALLUSA":
            rawUp  = Fundamental(type, "$UVOLUS");
            rawDn  = Fundamental(type, "$DVOLUS");
            rawAll = close("$TVOLUS");
        case "NYSE":
            rawUp  = Fundamental(type, "$UVOL");
            rawDn  = Fundamental(type, "$DVOL");
            rawAll = close("$TVOL");
        case "NASDAQ":
            rawUp  = Fundamental(type, "$UVOL/Q");
            rawDn  = Fundamental(type, "$DVOL/Q");
            rawAll = close("$TVOL/Q");
        case "S&P 500":
            rawUp  = Fundamental(type, "$UVOLSP");
            rawDn  = Fundamental(type, "$DVOLSP");
            rawAll = close("$TVOLSP");
        case "NASDAQ 100":
            rawUp  = Fundamental(type, "$UVOLND");
            rawDn  = Fundamental(type, "$DVOLND");
            rawAll = close("$TVOLND");
        case "RUSSELL 2000":
            rawUp  = Fundamental(type, "$UVOLRL");
            rawDn  = Fundamental(type, "$DVOLRL");
            rawAll = close("$TVOLRL");
        case "DJIA":
            rawUp  = Fundamental(type, "$UVOLI");
            rawDn  = Fundamental(type, "$DVOLI");
            rawAll = close("$TVOLI");
    }

    # Sanitize
    def up;
    def dn;
    def all;
    if Sanitize {
        up  = Value(rawUp);
        dn  = Value(rawDn);
        all = Value(rawAll);
    }
    else {
        up  = rawUp;
        dn  = rawDn;
        all = rawAll;
    }
###

### CALCULATIONS ###
    def isNewPeriod    = Periods(Period, SinceDate, CandlesAgo).isNewAndValid;
    def isActivePeriod = Periods(Period, SinceDate, CandlesAgo).isActive;

    # Tick
    def tickUp;
    def tickDn;
    def tickAll;
    if Chart().isIntrabar {
        tickUp  = up  - GetValue(up,  1);
        tickDn  = dn  - GetValue(dn,  1);
        tickAll = all - GetValue(all, 1);
    }
    else {
        tickUp  = up;
        tickDn  = dn;
        tickAll = all;
    }

    # Volume Graph
    def graphDn;
    def graphUp;
    if Chart().isIntrabar {
        graphDn = (Max(0, tickDn) + Min(0, tickUp)) * (tickAll / (tickUp + tickDn));
        graphUp = tickAll - graphDn;
    }
    else {
        graphDn = tickDn;
        graphUp = tickAll - graphDn;
    }

    # Value
    def rawValue;
    switch (Display) {
        case "Difference"  : rawValue = up - dn;
        case "Percentage"  : rawValue = If(all != 0, up / all, 0);
        case "Log Ratio"   : rawValue = If(up > 0 and dn > 0, Log(up / dn), 0);
        case "Z-Score"     : rawValue = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        case "Z-Sum"       : rawValue = If(up > 0 and dn > 0, ZScore(Log(up / dn)), 0);
        default            : rawValue = tickUp - tickDn;
    }

    # Cumulative
    def rawSum;
    if IsNaN(rawValue) {
        rawSum = GetValue(rawSum, 1);
    }
    else if isNewPeriod {
        rawSum = rawValue;
    }
    else {
        rawSum = GetValue(rawSum, 1) + rawValue;
    }

    # Filters
    def sum;
    def value;
    if !Chart().isValidBar or !isActivePeriod or IsNaN(rawValue) {
        value = Double.NaN;
        sum   = Double.NaN;
    }
    else {
        value = Smooth(Oscillate(rawValue, Oscillation), Smoothing);
        sum   = Smooth(Oscillate(rawSum,   Oscillation), Smoothing);
    }

    # Neutral
    def neutral;
    switch (Display) {
        case "Percentage" : neutral = 0.5;
        default           : neutral = 0;
    }
###

### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # Value
    plot UD = if Display == Display.Cumulative or Display == Display."Z-Sum" then sum else value;
    UD.SetHiding(Display == Display."Volume Graph" or Display == Display.Stack);
    UD.DefineColor("Up", Color.UPTICK);
    UD.DefineColor("Down", Color.DOWNTICK);
    UD.DefineColor("Neutral", Color.DARK_GRAY);
    UD.SetDefaultColor(UD.Color("Neutral"));
    UD.AssignValueColor(
        if Separators and isNewPeriod and GetValue(isActivePeriod, 1)
            then GlobalColor("Background")
        else if UD > neutral
            then UD.Color("Up")
        else if UD < neutral
            then UD.Color("Down")
            else UD.Color("Neutral")
    );

    # Stack
    plot GU = graphUp;
    GU.SetHiding(Display != Display.Stack);
    GU.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    GU.DefineColor("Value", Color.UPTICK);
    GU.SetDefaultColor(GU.Color("Value"));

    plot GD = -graphDn;
    GD.SetHiding(Display != Display.Stack);
    GD.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    GD.DefineColor("Value", Color.DOWNTICK);
    GD.SetDefaultColor(GD.Color("Value"));

    # Volume Graph
    plot D = graphDn;
    D.SetHiding(Display != Display."Volume Graph");
    D.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    D.DefineColor("Value", Color.DOWNTICK);
    D.SetDefaultColor(D.Color("Value"));

    plot A = tickAll;
    A.SetHiding(Display != Display."Volume Graph");
    A.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    A.DefineColor("Value", Color.UPTICK);
    A.SetDefaultColor(A.Color("Value"));

    # Line
    plot N = neutral;
    N.SetDefaultColor(GlobalColor("Line"));
    N.HideBubble();
    N.HideTitle();

    # Separators
    AddVerticalLine(
        visible = Separators and isNewPeriod and GetValue(isActivePeriod, 1),
        color   = GlobalColor("Separator"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );
###
