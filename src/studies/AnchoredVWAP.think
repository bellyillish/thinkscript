declare upper;

#include_once "../lib/Chart.think"
#include_once "../lib/Sanitizer.think"
#include_once "../lib/Period.think"

### INPUTS ###
    input Price = {
        "Open",
        "High",
        "Low",
        "Close",
        "HL2",
        "HLC3",
        "OHLC4",
        default "VWAP"
    };

    input Anchor = {
        default "Chart",
        "Daily",
        "Weekly",
        "Monthly",
        "MonthlyOPEX",
        "Quarterly",
        "QuarterlyOPEX",
        "Earnings",
        "Yearly",
        "CandlesAgo",
        "SinceDate"
    };

    input Deviations      = 0.0;
    @Date input SinceDate = 20250407;
    input CandlesAgo      = 252;
    input Separators      = yes;
    input Sanitize        = no;
###

### LOOKUPS ###
    def type;
    switch (Price) {
        case "VWAP"  : type = FundamentalType.VWAP;
        case "Close" : type = FundamentalType.CLOSE;
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
    }

    def data;
    if Sanitize {
        data = Sanitizer(Fundamental(type));
    }
    else {
        data = Fundamental(type);
    }
###

### CALCULATIONS ###
    def isActivePeriod = Period(Anchor, SinceDate, CandlesAgo).isActive;
    def isNewPeriod    = Period(Anchor, SinceDate, CandlesAgo).isNewAndValid;

    def volumeSum;
    def vwapSum;
    def devSum;
    if !Chart().isValidBar or !isActivePeriod {
        volumeSum = Double.NaN;
        vwapSum   = Double.NaN;
        devSum    = Double.NaN;
    }
    else if isNewPeriod {
        volumeSum = volume;
        vwapSum   = volume * data;
        devSum    = volume * Sqr(data);
    }
    else {
        volumeSum = GetValue(volumeSum, 1) + volume;
        vwapSum   = GetValue(vwapSum, 1) + volume * data;
        devSum    = GetValue(devSum, 1) + volume * Sqr(data);
    }

    def anchored  = vwapSum / volumeSum;
    def deviation = Sqrt(Max(devSum / volumeSum - Sqr(anchored), 0));
###

### PLOTS ###
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # VWAP
    plot AnchoredVWAP = anchored;
    AnchoredVWAP.DefineColor("Value", CreateColor(160, 160, 160));
    AnchoredVWAP.SetDefaultColor(AnchoredVWAP.Color("Value"));
    AnchoredVWAP.AssignValueColor(
        if isNewPeriod
            then GlobalColor("Background")
            else AnchoredVWAP.Color("Value")
    );

    # Deviations
    plot Upper = anchored + Deviations * deviation;
    Upper.DefineColor("Value", CreateColor(48, 48, 48));
    Upper.SetDefaultColor(Upper.Color("Value"));
    Upper.SetHiding(Deviations <= 0);

    plot Lower = anchored + -Deviations * deviation;
    Lower.DefineColor("Value", CreateColor(48, 48, 48));
    Lower.SetDefaultColor(Lower.Color("Value"));
    Lower.SetHiding(Deviations <= 0);

    # Separators
    AddVerticalLine(
        visible = Separators and isNewPeriod and GetValue(isActivePeriod, 1),
        color   = GlobalColor("Separator"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );
###
