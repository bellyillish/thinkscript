declare upper;


### SCRIPTS ###
    script Date {
        def yyyymmdd = GetYYYYMMDD();
        def yyyy     = GetYear();
        def mm       = GetMonth();
        def doy      = GetDay();
        def dom      = GetDayOfMonth(yyyymmdd);
        def dow      = GetDayOfWeek(yyyymmdd);
        def dowj1    = GetDayOfWeek(yyyy * 10000 + 0101);
        def ww       = Ceil((doy + 1 + if dowj1 >= 5 then dowj1 - 7 else dowj1) / 7);
        def q        = Ceil(mm / 3);
        def yyyyq    = yyyy * 10 + q;
        def yyyymm   = yyyy * 100 + mm;
        def yyyyww   = yyyy * 100 + ww;

        def isAfterOpex    = (dom - 14 - if dow > 5 then dow - 5 else dow + 2) > 0;
        def isEndOfQuarter = mm % 3 == 0;

        def qOpex = if q + isAfterOpex * isEndOfQuarter <= 4
            then q + isAfterOpex * isEndOfQuarter
            else 1;

        def mmOpex = if mm + isAfterOpex <= 12
            then mm + isAfterOpex
            else 1;

        def yyyyOpex = if mm + isAfterOpex <= 12
            then yyyy
            else yyyy + 1;

        def yyyyqOpex  = yyyyOpex * 10  + qOpex;
        def yyyymmOpex = yyyyOpex * 100 + mmOpex;

        plot P = yyyymmdd;
        P.hide();
    }

    script Chart {
        input Validator = "$DJI";

        def aggregation   = GetAggregationPeriod();
        def isIntraday    = aggregation <  AggregationPeriod.DAY;
        def isDaily       = aggregation == AggregationPeriod.DAY;
        def isMultiDay    = aggregation >  AggregationPeriod.DAY;
        def isIntradayBar = isIntraday and Date() == Date()[1];
        def number        = BarNumber();
        def startBar      = First(number);
        def endBar        = HighestAll(number * !IsNaN(close(Validator)));
        def isValidBar    = number between startBar and endBar;
        def startOffset   = number - startBar;
        def endOffset     = number - endBar;
        def isStart       = number == startBar;
        def isEnd         = number == endBar;

        plot P = Double.NaN;
        P.hide();
    }

    script Value {
        input raw    = 0;
        input minRaw = 0;
        input maxRaw = 0;

        def minValue = if minRaw < 0 then minRaw else -999999999999999;
        def maxValue = if maxRaw > 0 then maxRaw else  999999999999999;

        def isValid;
        if IsNaN(raw) or IsInfinite(raw) {
            isValid = no;
        }
        else {
            isValid = raw between minValue and maxValue;
        }

        def value = if isValid or !Chart().isValidBar
            then raw
            else value[1];

        plot P = Double.NaN;
        P.hide();
    }

    script Periods {
        input period  = "Chart";
        input since   = 20250407;
        input candles = 100;

        def isStart     = Chart().isStart;
        def endBar      = Chart().endBar;
        def aggregation = GetAggregationPeriod();

        def isNew;
        if period == "Daily" and aggregation <= AggregationPeriod.FOUR_HOURS {
            isNew = isStart or Date() != Date()[1];
        }
        else if period == "Weekly" and aggregation <= AggregationPeriod.TWO_DAYS {
            isNew = isStart or Date().yyyyww != Date().yyyyww[1];
        }
        else if period == "Monthly" and aggregation <= AggregationPeriod.WEEK {
            isNew = isStart or Date().yyyymm != Date().yyyymm[1];
        }
        else if period == "MonthlyOPEX" and aggregation <= AggregationPeriod.WEEK {
            isNew = isStart or Date().yyyymmOpex != Date().yyyymmOpex[1];
        }
        else if period == "Quarterly" and aggregation <= AggregationPeriod.MONTH {
            isNew = isStart or Date().yyyyq != Date().yyyyq[1];
        }
        else if period == "QuarterlyOPEX" and aggregation <= AggregationPeriod.MONTH{
            isNew = isStart or Date().yyyyqOpex != Date().yyyyqOpex[1];
        }
        else if period == "Yearly" and aggregation <= AggregationPeriod.QUARTER {
            isNew = isStart or Date().yyyy != Date().yyyy[1];
        }
        else if period == "SinceDate" {
            isNew = Date() >= since and (Date()[1] < since or isStart);
        }
        else if period == "CandlesAgo" {
            isNew = isStart and candles > endBar or BarNumber() + candles == endBar;
        }
        else {
            isNew = isStart;
        }

        def isActive = isNew or isActive[1];

        plot P = isNew;
        P.hide();
    }
###


### INPUTS ###
    input Quote = {
        default "Close",
        "Open",
        "High",
        "Low",
        "HL2",
        "HLC3",
        "OHLC4",
        "VWAP"
    };

    input Deviations = {
        default "0",
        "1",
        "2",
        "3",
        "4"
    };

    @Date input SinceDate = 19020101;
    input Sanitize        = no;
    input ExtendLeft      = no;
    input ExtendRight     = no;
###


### LOOKUPS ###
    def type;
    switch (Quote) {
        case "Close" : type = FundamentalType.CLOSE;
        case "Open"  : type = FundamentalType.OPEN;
        case "High"  : type = FundamentalType.HIGH;
        case "Low"   : type = FundamentalType.LOW;
        case "HL2"   : type = FundamentalType.HL2;
        case "HLC3"  : type = FundamentalType.HLC3;
        case "OHLC4" : type = FundamentalType.OHLC4;
        case "VWAP"  : type = FundamentalType.VWAP;
    }

    def data;
    if Sanitize {
        data = Value(Fundamental(type)).value;
    }
    else {
        data = Fundamental(type);
    }
###


### CALCULATIONS ###
    def mean = Exp(
        InertiaAll(
            data          = Log(data),
            startDate     = SinceDate,
            extendToLeft  = ExtendLeft,
            extendToRight = ExtendRight
        )
    );

    def dev = Exp(
        StDevAll(
            data          = Log(data / mean),
            StartDate     = SinceDate,
            extendToLeft  = ExtendLeft,
            extendToRight = ExtendRight
        )
    );
###


### PLOTS ###
  plot Regression = mean;
  Regression.DefineColor("Value", Color.GRAY);
  Regression.SetDefaultColor(Regression.Color("Value"));

  ### 1 stdev
  plot Up1 = mean * dev;
  Up1.SetHiding(Deviations < 1);
  Up1.DefineColor("Value", Color.DARK_GRAY);
  Up1.SetDefaultColor(Up1.Color("Value"));

  plot Down1 = mean / dev;
  Down1.SetHiding(Deviations < 1);
  Down1.DefineColor("Value", Color.DARK_GRAY);
  Down1.SetDefaultColor(Down1.Color("Value"));

  ### 2 stdevs
  plot Up2 = mean * Power(dev, 2);
  Up2.SetHiding(Deviations < 2);
  Up2.DefineColor("Value", Color.DARK_GRAY);
  Up2.SetDefaultColor(Up2.Color("Value"));

  plot Down2 = mean / Power(dev, 2);
  Down2.SetHiding(Deviations < 2);
  Down2.DefineColor("Value", Color.DARK_GRAY);
  Down2.SetDefaultColor(Down2.Color("Value"));

  ### 3 stdevs
  plot Up3 = mean * Power(dev, 3);
  Up3.SetHiding(Deviations < 3);
  Up3.DefineColor("Value", Color.DARK_GRAY);
  Up3.SetDefaultColor(Up3.Color("Value"));

  plot Down3 = mean / Power(dev, 3);
  Down3.SetHiding(Deviations < 3);
  Down3.DefineColor("Value", Color.DARK_GRAY);
  Down3.SetDefaultColor(Down3.Color("Value"));

  ### 4 stdevs
  plot Up4 = mean * Power(dev, 4);
  Up4.SetHiding(Deviations < 4);
  Up4.DefineColor("Value", Color.DARK_GRAY);
  Up4.SetDefaultColor(Up4.Color("Value"));

  plot Down4 = mean / Power(dev, 4);
  Down4.SetHiding(Deviations < 4);
  Down4.DefineColor("Value", Color.DARK_GRAY);
  Down4.SetDefaultColor(Down4.Color("Value"));
###
