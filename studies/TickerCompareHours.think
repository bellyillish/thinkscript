declare lower;
declare real_size;


### SCRIPTS ###
    script Date {
        def yyyymmdd = GetYYYYMMDD();
        def yyyy     = GetYear();
        def mm       = GetMonth();
        def doy      = GetDay();
        def dom      = GetDayOfMonth(yyyymmdd);
        def dow      = GetDayOfWeek(yyyymmdd);
        def dowj1    = GetDayOfWeek(yyyy * 10000 + 0101);
        def ww       = GetWeek();
        def q        = Ceil(mm / 3);
        def yyyyq    = yyyy * 10 + q;
        def yyyymm   = yyyy * 100 + mm;

        def minus = if yyyy > yyyy[1] and ww >= ww[1] then 1
            else if ww != ww[1] then 0
            else minus[1];

        def yyyyww = (yyyy - minus) * 100 + ww;

        def isAfterOpex    = (dom - 14 - if dow > 5 then dow - 5 else dow + 2) > 0;
        def isEndOfQuarter = mm % 3 == 0;

        def qOpex = if q + isAfterOpex * isEndOfQuarter <= 4
            then q + isAfterOpex * isEndOfQuarter
            else 1;

        def mmOpex = if mm + isAfterOpex <= 12
            then mm + isAfterOpex
            else 1;

        def yyyyOpex = if mm + isAfterOpex <= 12
            then yyyy
            else yyyy + 1;

        def yyyyqOpex  = yyyyOpex * 10  + qOpex;
        def yyyymmOpex = yyyyOpex * 100 + mmOpex;

        def lastEarnings = if HasEarnings(EarningTime.BEFORE_MARKET) or HasEarnings(EarningTime.AFTER_MARKET)[1]
            then GetYYYYMMDD()
            else lastEarnings[1];

        plot P = yyyymmdd;
        P.hide();
    }

    script Chart {
        input Validator = "$DJI";

        def aggregation   = GetAggregationPeriod();
        def isIntraday    = aggregation <  AggregationPeriod.DAY;
        def isDaily       = aggregation == AggregationPeriod.DAY;
        def isMultiDay    = aggregation >  AggregationPeriod.DAY;
        def isIntradayBar = isIntraday and Date() == Date()[1];
        def number        = BarNumber();
        def startBar      = First(number);
        def endBar        = HighestAll(number * !IsNaN(close(Validator)));
        def isValidBar    = number between startBar and endBar;
        def startOffset   = number - startBar;
        def endOffset     = number - endBar;
        def isStart       = number == startBar;
        def isEnd         = number == endBar;

        plot P = Double.NaN;
        P.hide();
    }

    script Value {
        input raw    = 0;
        input minRaw = 0;
        input maxRaw = 0;

        def minValue = if minRaw < 0 then minRaw else -999999999999999;
        def maxValue = if maxRaw > 0 then maxRaw else  999999999999999;

        def isValid;
        if IsNaN(raw) or IsInfinite(raw) {
            isValid = no;
        }
        else {
            isValid = raw between minValue and maxValue;
        }

        def value = if isValid or !Chart().isValidBar
            then raw
            else value[1];

        plot P = value;
        P.hide();
    }

    script Filters {
        input raw         = 0;
        input oscillation = 0;
        input smoothing   = 0;

        def oscValue;
        if oscillation > 1 {
            oscValue = raw - Average(raw, oscillation);
        }
        else {
            oscValue = raw;
        }

        def value;
        if smoothing > 1 {
            value = Inertia(oscValue, smoothing);
        }
        else {
            value = oscValue;
        }

        plot P = value;
        P.Hide();
    }

    script Periods {
        input period  = "Chart";
        input since   = 20250407;
        input candles = 100;

        def isStart     = Chart().isStart;
        def endBar      = Chart().endBar;
        def aggregation = GetAggregationPeriod();

        def isNew;
        if period == "Daily" and aggregation <= AggregationPeriod.FOUR_HOURS {
            isNew = isStart or Date() != Date()[1];
        }
        else if period == "Weekly" and aggregation <= AggregationPeriod.TWO_DAYS {
            isNew = isStart or Date().yyyyww != Date().yyyyww[1];
        }
        else if period == "Monthly" and aggregation <= AggregationPeriod.WEEK {
            isNew = isStart or Date().yyyymm != Date().yyyymm[1];
        }
        else if period == "MonthlyOPEX" and aggregation <= AggregationPeriod.WEEK {
            isNew = isStart or Date().yyyymmOpex != Date().yyyymmOpex[1];
        }
        else if period == "Quarterly" and aggregation <= AggregationPeriod.MONTH {
            isNew = isStart or Date().yyyyq != Date().yyyyq[1];
        }
        else if period == "QuarterlyOPEX" and aggregation <= AggregationPeriod.MONTH {
            isNew = isStart or Date().yyyyqOpex != Date().yyyyqOpex[1];
        }
        else if period == "Earnings" and aggregation <= AggregationPeriod.MONTH {
            isNew = isStart or Date().lastEarnings != Date().lastEarnings[1];
        }
        else if period == "Yearly" and aggregation <= AggregationPeriod.QUARTER {
            isNew = isStart or Date().yyyy != Date().yyyy[1];
        }
        else if period == "SinceDate" {
            isNew = Date() >= since and (Date()[1] < since or isStart);
        }
        else if period == "CandlesAgo" {
            isNew = isStart and candles > endBar or BarNumber() + candles == endBar;
        }
        else {
            isNew = isStart;
        }

        def isActive = isNew or isActive[1];

        plot P = isNew;
        P.hide();
    }
###


### INPUTS ###
    input Ticker = "";

    input Mode = {
        default "Combined",
        "Separate",
        "Cloud"
    };

    input Display = {
        default "Logarithmic",
        "Relative",
        "Absolute",
        "Tick"
    };

    input Period = {
        default "Chart",
        "Daily",
        "Weekly",
        "Monthly",
        "MonthlyOPEX",
        "Quarterly",
        "QuarterlyOPEX",
        "Earnings",
        "Yearly",
        "CandlesAgo",
        "SinceDate"
    };

    @Date input SinceDate = 20250407;
    input CandlesAgo      = 100;
    input Separators      = yes;
    input Sanitize        = no;
    input Oscillation     = 0;
    input Smoothing       = 0;
###


### LOOKUPS ###
    def rawOpen;
    def rawClose;
    if Ticker != "" {
        rawOpen  = open(Ticker);
        rawClose = close(Ticker);
    }
    else {
        rawOpen  = open;
        rawClose = close;
    }

    # Sanitize
    def dataOpen;
    def dataClose;
    if Sanitize {
        dataOpen  = Value(rawOpen);
        dataClose = Value(rawClose);
    }
    else {
        dataOpen  = rawOpen;
        dataClose = rawClose;
    }
###


### CALCULATIONS ###
    def isNewPeriod    = Periods(Period, SinceDate, CandlesAgo).isNew;
    def isActivePeriod = Periods(Period, SinceDate, CandlesAgo).isActive;

    # Logarithmic
    def logOpen;
    def logClose;
    if Display == Display.Logarithmic {
        logOpen  = Log(dataOpen);
        logClose = Log(dataClose);
    }
    else {
        logOpen  = dataOpen;
        logClose = dataClose;
    }

    # Open
    def startETH = logClose[1];
    def startRTH;
    if !Chart().isIntradayBar and !Chart().isMultiDay {
        startRTH = logOpen;
    }
    else {
        startRTH = logClose[1];
    }

    # Close
    def endRTH = logClose;
    def endETH;
    if Chart().isIntradayBar {
        endETH = logClose[1];
    }
    else if !Chart().isMultiDay {
        endETH = logOpen;
    }
    else {
        endETH = logClose;
    }

    # Tick
    def rawTickRTH;
    def rawTickETH;
    def rawTick;
    if Display == Display.Relative {
        rawTickRTH = endRTH / startRTH - 1;
        rawTickETH = endETH / startETH - 1;
        rawTick    = (endRTH / startRTH) / (endETH / startETH) - 1;
    }
    else {
        rawTickRTH = endRTH - startRTH;
        rawTickETH = endETH - startETH;
        rawTick    = rawTickRTH - rawTickETH;
    }

    # Cumulative
    def rawSumRTH;
    def rawSumETH;
    def rawSum;
    if isNewPeriod {
        rawSumRTH = rawTickRTH;
        rawSumETH = rawTickETH;
        rawSum    = rawTick;
    }
    else if Display == Display.Relative {
        rawSumRTH = (rawSumRTH[1] + 1) * (rawTickRTH + 1) - 1;
        rawSumETH = (rawSumETH[1] + 1) * (rawTickETH + 1) - 1;
        rawSum    = (rawSum[1] + 1) * (rawTick + 1) - 1;
    }
    else {
        rawSumRTH = rawSumRTH[1] + rawTickRTH;
        rawSumETH = rawSumETH[1] + rawTickETH;
        rawSum    = rawSum[1] + rawTick;
    }

    # Filters
    def sumRTH;
    def sumETH;
    def sum;
    def tick;
    if !Chart().isValidBar or !isActivePeriod {
        sumRTH  = Double.NaN;
        sumETH  = Double.NaN;
        sum     = Double.NaN;
        tick    = Double.NaN;
    }
    else {
        sumRTH  = Filters(rawSumRTH, Oscillation, Smoothing);
        sumETH  = Filters(rawSumETH, Oscillation, Smoothing);
        sum     = Filters(rawSum, Oscillation, Smoothing);
        tick    = Filters(rawTick, Oscillation, Smoothing);
    }
###


### PLOTS ###
    DefineGlobalColor("Line",       CreateColor(48, 48, 48));
    DefineGlobalColor("Separator",  CreateColor(32, 32, 32));
    DefineGlobalColor("Background", CreateColor(12, 12, 12));

    # Tick
    plot T = tick;
    T.SetHiding(Display != Display.Tick);
    T.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
    T.DefineColor("Up", CreateColor(230, 205, 80));
    T.DefineColor("Down", CreateColor(77, 166, 255));
    T.DefineColor("Neutral", Color.DARK_GRAY);
    T.SetDefaultColor(T.Color("Neutral"));
    T.AssignValueColor(
        if isNewPeriod and isActivePeriod[1]
            then GlobalColor("Background")
        else if tick > 0
            then T.Color("Up")
        else if tick < 0
            then T.Color("Down")
            else T.Color("Neutral")
    );

    # Cumulative
    plot S = sum;
    S.SetHiding(Display == Display.Tick or Mode != Mode.Combined);
    S.DefineColor("Up", CreateColor(230, 205, 80));
    S.DefineColor("Down", CreateColor(77, 166, 255));
    S.DefineColor("Neutral", Color.DARK_GRAY);
    S.SetDefaultColor(S.Color("Neutral"));
    S.AssignValueColor(
        if isNewPeriod and isActivePeriod[1]
            then GlobalColor("Background")
        else if sum > 0
            then S.Color("Up")
        else if sum < 0
            then S.Color("Down")
            else S.Color("Neutral")
    );

    # Cumulative RTH
    plot RTH = sumRTH;
    RTH.SetHiding(Display == Display.Tick or Mode == Mode.Combined);
    RTH.DefineColor("Value", CreateColor(230, 205, 80));
    RTH.SetDefaultColor(RTH.Color("Value"));
    RTH.AssignValueColor(
        if isNewPeriod and isActivePeriod[1]
            then GlobalColor("Background")
            else RTH.Color("Value")
    );

    # Cumulative ETH
    plot ETH = sumETH;
    ETH.SetHiding(Display == Display.Tick or Mode == Mode.Combined);
    ETH.DefineColor("Value", CreateColor(77, 166, 255));
    ETH.SetDefaultColor(ETH.Color("Value"));
    ETH.AssignValueColor(
        if isNewPeriod and isActivePeriod[1]
            then GlobalColor("Background")
            else ETH.Color("Value")
    );

    # Cloud
    def cloudRTH = sumRTH;
    def cloudETH = sumETH;
    AddCloud(
        data1 = if Mode == Mode.Cloud and Display != Display.Tick
            then cloudRTH
            else Double.NaN,
        data2 = if Mode == Mode.Cloud and Display != Display.Tick
            then cloudETH
            else Double.NaN,
        color1 = RTH.Color("Value"),
        color2 = ETH.Color("Value")
    );

    # Line
    plot N = 0;
    N.SetDefaultColor(GlobalColor("Line"));
    N.HideBubble();
    N.HideTitle();
    N.Hide();

    # Separators
    AddVerticalLine(
        visible = Separators and isNewPeriod and isActivePeriod[1],
        color   = GlobalColor("Separator"),
        stroke  = Curve.SHORT_DASH,
        text    = ""
    );
###
